plugins {
    id "java"
    id "org.jetbrains.kotlin.jvm" version "1.9.20"
    id "com.github.johnrengelman.shadow" version "7.1.0"
    id "io.papermc.paperweight.userdev" version "1.5.10"
}

group = "prisongame"
version = "0.1.0"

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "md_5-public"
        url = "https://repo.md-5.net/content/groups/public/"
    }
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        name = "dmulloy2-repo"
        url = "https://repo.dmulloy2.net/repository/public/"
    }
    maven {
        name = "jitpack"
        url = "https://jitpack.io/"
    }
}

dependencies {
    paperweight.paperDevBundle("1.20.1-R0.1-SNAPSHOT")
    implementation "cc.ekblad:4koma:1.2.0"

    compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.9.20"
    compileOnly("LibsDisguises:LibsDisguises:10.0.31") {
        exclude group: "org.spigotmc"
    }
    compileOnly files("libs/CustomBansPlus-3.6.jar")
    compileOnly "net.luckperms:api:5.4"
    compileOnly "net.dv8tion:JDA:5.0.0-beta.18"
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset "UTF-8"

    filesMatching("plugin.yml") {
        expand props
    }
}

shadowJar {
    dependencies {
        exclude(dependency {
            it.moduleGroup == "org.jetbrains.kotlin"
        })
    }

    def reloc = { String pkg -> relocate(pkg, "prisongame.prisongame.libs." + pkg) }

    reloc("cc.ekblad")
}

kotlin {
    jvmToolchain(17)
}